# Copyright 2016 Husky Team
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
# http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.


project(husky)
cmake_minimum_required(VERSION 3.0.2)

# CMake Options
set(CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_STANDARD 14)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR})
if(CMAKE_COMPILER_IS_GNUCXX)
    set(CMAKE_CXX_FLAGS_DEBUG "-g -DHUSKY_DEBUG_MODE")
    set(CMAKE_CXX_FLAGS_RELEASE "-O3 -w")
    set(CMAKE_CXX_FLAGS ${CMAKE_CXX_FLAGS} "-Wno-deprecated-declarations")
endif()

# Dependencies
include(cmake/dep.cmake)
include(cmake/utils.cmake)
include(cmake/external.cmake)

# Boost Libraries
find_package(Threads)
find_package(Boost 1.58.0 COMPONENTS system filesystem regex thread program_options date_time chrono)

# External Includes
set(HUSKY_EXTERNAL_INCLUDE ${Boost_INCLUDE_DIRS} ${ZMQ_INCLUDE_DIR} ${GLOG_INCLUDE})

# External Libraries
set(HUSKY_EXTERNAL_LIB ${ZMQ_LIBRARY} ${TCMALLOC_LIBRARY} ${CMAKE_THREAD_LIBS_INIT} ${Boost_LIBRARIES} ${GLOG_LIBRARY})

# External Definitions
set(HUSKY_EXTERNAL_DEFINITION "")

# libhdfs3
if(LIBHDFS3_FOUND)
    list(APPEND HUSKY_EXTERNAL_INCLUDE ${LIBHDFS3_INCLUDE_DIR})
    list(APPEND HUSKY_EXTERNAL_LIB ${LIBHDFS3_LIBRARY})
    list(APPEND HUSKY_EXTERNAL_DEFINITION ${LIBHDFS3_DEFINITION})
endif(LIBHDFS3_FOUND)

# MongoDB
if(MONGOCLIENT_FOUND)
    list(APPEND HUSKY_EXTERNAL_INCLUDE ${MONGOCLIENT_INCLUDE_DIR})
    list(APPEND HUSKY_EXTERNAL_LIB ${RT_LIBRARY} ${MONGOCLIENT_LIBRARY})
    list(APPEND HUSKY_EXTERNAL_DEFINITION ${MONGOCLIENT_DEFINITION})
endif(MONGOCLIENT_FOUND)

# Thrift
if(THRIFT_FOUND)
    list(APPEND HUSKY_EXTERNAL_INCLUDE ${THRIFT_INCLUDE_DIR})
    list(APPEND HUSKY_EXTERNAL_LIB ${THRIFT_LIBRARY})
    list(APPEND HUSKY_EXTERNAL_DEFINITION ${THRIFT_DEFINITION})
endif(THRIFT_FOUND)

if(WIN32)
    list(APPEND HUSKY_EXTERNAL_LIB wsock32 ws2_32)
endif()

add_definitions(${HUSKY_EXTERNAL_DEFINITION})

set(HUSKY_EXTERNAL_INCLUDE ${HUSKY_EXTERNAL_INCLUDE} CACHE INTERNAL "")
set(HUSKY_EXTERNAL_LIB ${HUSKY_EXTERNAL_LIB} CACHE INTERNAL "")
set(HUSKY_EXTERNAL_DEFINITION ${HUSKY_EXTERNAL_DEFINITION} CACHE INTERNAL "")

add_subdirectory(base)
add_subdirectory(core)
add_subdirectory(io)
add_subdirectory(lib)
add_subdirectory(examples)
add_subdirectory(test)
add_subdirectory(benchmarks)

# libhusky.a
include_directories(${PROJECT_SOURCE_DIR} ${HUSKY_EXTERNAL_INCLUDE})
set(CPP_FILES ${BASE_CPP_FILES} ${CORE_CPP_FILES} ${IO_CPP_FILES} ${LIB_CPP_FILES})
file(GLOB_RECURSE UNITTESTS *_unittest.cpp)
list(REMOVE_ITEM CPP_FILES ${UNITTESTS})
add_library(husky SHARED ${CPP_FILES})
husky_default_properties(husky)

add_subdirectory(master)
